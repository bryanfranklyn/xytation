## note - add as script with name (Auto-retrieve-bad-IPs) ##
## note - add as full user (root user) ##
## note - this does add almost 6000 ip's ##

:log info "Updating database"
:delay 60
ip firewall address-list
:local update do={
:do {
:local data ([:tool fetch url=$url output=user as-value]->"data")
remove [find list=blacklist comment=$description]
:while ([:len $data]!=0) do={
:if ([:pick $data 0 [:find $data "\n"]]~"^[0-9]{1,3}\\.[0-9]{1,3}\\.[0-9]{1,3}\\.[0-9]{1,3}") do={
:do {add list=blacklist address=([:pick $data 0 [:find $data $delimiter]].$cidr) comment=$description timeout=1d} on-error={}
}
:set data [:pick $data ([:find $data "\n"]+1) [:len $data]]
}
} on-error={:log warning "Address list <$description> update failed"}
}

$update url=https://feodotracker.abuse.ch/downloads/ipblocklist_recommended.txt description="BotNetC&C" delimiter=("\r")
$update url=https://raw.githubusercontent.com/stamparm/ipsum/master/levels/6.txt description="Blacklist level6" delimiter=("\n")
$update url=https://www.spamhaus.org/drop/drop.txt description="Spamhaus DROP" delimiter=("\_")
$update url=https://sslbl.abuse.ch/blacklist/sslipblacklist.txt description="Abuse.ch BotNet SSL" delimiter=("\r")
$update url=https://www.spamhaus.org/drop/edrop.txt description="Spamhaus EDROP" delimiter=("\_")
$update url=https://feodotracker.abuse.ch/downloads/ipblocklist.txt description="Feodo C&C Drop" delimiter=("\r")
$update url=https://raw.githubusercontent.com/pallebone/StrictBlockPAllebone/master/BlockIP.txt description="Pallebone IPs" delimiter=("\r")
:log info "Update Complete"
